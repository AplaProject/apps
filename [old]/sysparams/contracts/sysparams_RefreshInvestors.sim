contract sysparams_RefreshInvestors {
    data {
        role_id int
    }
    
    conditions{
        $limitInvestors = 3
        
        $founder = EcosysParam("founder_account")
        if Str($key_id) != $founder{
            info "You can not start to voting"
        }
        
        $role = DBFind("roles_list").Where("id=?", $role_id).Row()
        if !$role{
            warning "Role not found"
        }
        
        $role_name = $role["role_name"]
        $role_type = Int($role["role_type"])
        
    }
    
    action{
        // remove old investors
        DBUpdateExt("roles_assign", "role_id", $role_id, "delete", 1)
        
        // get appointer
        $appointerFind = DBFind("member").Where("member_id=?",$key_id)
        $appointer = $appointerFind[0]
        
        // assign new investors
        $investorsFind = DBFind("keys").Order("amount desc, id").Limit($limitInvestors)
        $lenInvestors = Len($investorsFind)
        
        $i = 0
        while($i < $lenInvestors){
            $investor = $investorsFind[$i]
            $memberFind = DBFind("member").Where("member_id=?", $investor["id"])
            if Len($memberFind) > 0{
                $member = $memberFind[0]
                DBInsert("roles_assign", `role_id,role_type,role_name,member_id,member_name,member_avatar,appointed_by_id,appointed_by_name,timestamp date_start,delete`, $role_id, $role_type, $role_name, $investor["id"], $member["member_name"], $member["avatar"], $key_id, $appointer["member_name"], $block_time, 0)
            }
            $i =$i+1
        }
    }
}