contract Roles_CheckAccess {
    data {
        rid int
    }
    conditions {
        var role map
        role = DBFind("roles").Columns("id,role_name,deleted,roles_access->rids,role_type,creator->member_id,company_id").Where("id=?", $rid).Row()
        if !role {
            warning Sprintf("%v: Role not found", $this_contract)
        }
        
        if role["creator.member_id"] == $key_id {
            return
        }

        var rids rawRids array, i int
        rawRids = Split(role["roles_access.rids"], ",")
        while i < Len(rawRids){
            if rawRids[i] > 0 {
                rids = Append(rids, rawRids[i])
            }
            i=i+1
        }
        i=0
        while i<Len(rids){
            if rids[i] == $role_id {
                return
            }
            i=i+1
        }

        if role["role_type"] == 4 {
            // company role
            if role["company_id"] == $key_id {
                return
            }else{
                warning "Sorry, you are not the creator of this company"
            }
        }
        warning Sprintf("Sorry, you don't have permission to change this role (%q)", role["role_name"])
    }
}