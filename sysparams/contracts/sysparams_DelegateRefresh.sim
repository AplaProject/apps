contract sysparams_DelegateRefresh{
    func NotInArray(val string, arr array) bool{
        var i int
        // info arr
        while i< Len(arr){
            if val == arr[i]{
                return false
            }
            i=i+1
        }
        return true
    }
    
    func conditions{
        $roleName = "Delegate"
        $delegateParam = "delegate_account"
        $ecosParamTbl = "parameters"
        
        $raTbl = "roles_assign"
        $rolesTbl = "roles_list"
        
        // get role Id and Type
        $role = DBFind($rolesTbl).Where("role_name=?", $roleName).Order("id").Row()
        if !$role{
            return "Role not found"
        }
        $roleId = $role["id"]
        $roleType = $role["role_type"]
        
    }
    
    func action{
        // get delegates ids
        var delegateIds array
        var dIndex int
        $i = 1
        while $i <= $ecosystem_id {
            $delegate = DBFind($ecosParamTbl).Where("name=?",$delegateParam).Ecosystem($i).Row()
            if !$delegate{
                $delegate = DBFind($ecosParamTbl).Where("name='founder_account'").Ecosystem($i).Row()
            }
            $delegateValue = $delegate["value"]
            if NotInArray($delegateValue, delegateIds){
                delegateIds[dIndex] = $delegateValue
                dIndex = dIndex + 1
            }
            $i = $i + 1
        }
        
        // remove old role assigns
        $currentDelegatesFind = DBFind($raTbl).Where("role_id=? and delete=0", $roleId)
        $i = 0
        while $i < Len($currentDelegatesFind){
            $currentDelegate = $currentDelegatesFind[$i]
            if NotInArray($currentDelegate["member_id"], delegateIds){
                $Id = $currentDelegate["id"]
                DBUpdate($raTbl, Int($Id), "delete", 1)
            }
            $i = $i + 1
        }
        
        // assign new roles
        $i = 0
        while $i < Len(delegateIds) {
            $delegateId = Int(delegateIds[$i])
            $memberId = DBFind("member").WhereId($delegateId).One("id")
            if $memberId{
                // check role dup
                $assigned = DBFind($raTbl).Where("member_id=? and role_id=? and delete=0", $delegateId, $roleId).Row()
                if !$assigned{
                    sysparams_RoleAssign("RoleName,KeyId", $roleName, Int($memberId))
                }
            }
            $i =$i+1
        }
    }
}
