contract RUpdate {
    data {
        Member string
        Amount int
        EventType int "optional"
        EventId int
        LastEvent string "optional"
    }

    func updateRating(){
        var accruals array total money offset limit i accrualsLen int acc map
        limit = 250
        while true {
            accruals = DBFind("@1r_accruals").Where({"ecosystem": $ecosystem_id, "member": $Member}).Limit(limit).Offset(offset)
            accrualsLen = Len(accruals)
            if accrualsLen == 0 {
                break
            }
            i = 0
            while i < accrualsLen {
                acc = accruals[i]
                total = total + Money(acc["amount"])
                i = i + 1
            }
            offset = offset + limit
        }
        var rating m map
        rating = DBFind("@1r_ratings").Where({"member": $Member, "ecosystem": $ecosystem_id}).Row()
        m = {amount:total, updated_at:$block_time}
        if rating {
            if Money(rating["amount"]) != total {
                DBUpdate("@1r_ratings", Int(rating["id"]), m)
            }
        } else {
            m["member"] = $Member
            m["ecosystem"] = $ecosystem_id
            DBInsert("@1r_ratings", m)
        }
    }

    conditions{
        $app = DBFind("@1applications").Where({"ecosystem": $ecosystem_id, "name": "Ratings"}).One("id")
        var admin_ids string
        admin_ids = AppParam(Int($app), "ratings_admin_role", $ecosystem_id)
        var rids array
        rids = JSONDecode("["+admin_ids+"]")
        if !DBFind("@1roles_participants").Where({"ecosystem": $ecosystem_id, "role->id": {"$in": rids}, "member->member_id": $key_id, "deleted": 0}).Row() {
            warning LangRes("@1access_denied", "en")
        }

        if $Amount == 0 {
            warning "Invalid amount"
        }

        var id int
        id = AddressToId($Member)
        if !DBFind("@1keys").Where({"ecosystem": $ecosystem_id, "id": id}).Row(){
            warning "Wallet not found"
        }

        if $LastEvent == "true" {
            var event map
            event = DBFind("@1e_events").WhereId($EventId).Row()
            if !event {
                warning "Event not found"
            }
            $EventType = Int(event["type"])
        }
    }

    action {
        var m map manager string
        manager = IdToAddress($key_id)
        m = {member:$Member, amount:$Amount, manager:manager, created_at:$block_time, event_id:$EventId, event_type:$EventType, ecosystem:$ecosystem_id}
        DBInsert("@1r_accruals", m)
        updateRating()
    }
}