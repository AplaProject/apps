contract PoaAdd {
    data {
        Recipient string
        DateExpiration string "optional"
        TimeExpiration string "optional"
        TemplateId int
        ParamArr array
        ValueArr array
    }

    func getMemberInfo(kid int) map {
        var res member map
        member = DBFind("@1members").Where({ecosystem:$ecosystem_id, id:kid}).Row()
        if member {
            res["member_id"] = member["id"]
            res["member_name"] = member["member_name"]
            res["image_id"] = member["image_id"]
        } else {
            member = DBFind("@1keys").Where({ecosystem:$ecosystem_id, id:kid}).Row()
            if member {
                res["member_id"] = member["id"]
                res["member_name"] = IdToAddress(Int(member["id"]))
                res["image_id"] = 0
            }
        }
        return res
    }

    func trimZeroTime(s string) string {
        if Contains(s, "T00:00:00Z") {
            s = s Replace(s, "T00:00:00Z", "")
        }
        return s
    }

    func dateAddTime(d, t string) string {
        var dt string
        if Size(t) == 5 {
            dt = Sprintf("%v %v:00", trimZeroTime(d), t)
        }
        return dt
    }

    conditions {
        //check date
        $date_expiration = dateAddTime($DateExpiration, $TimeExpiration)
        if UnixDateTime($date_expiration) == 0 {        // invalid datetimes
            error LangRes("@1invalid_expiration_date", "en") 
        }

        //check the recipient key
        $recipient_id = AddressToId($Recipient)
        if $recipient_id == 0 {
            error Sprintf(LangRes("@1recipient_x_invalid", "en"), $Recipient)
        }

        //check template
        $template_map = DBFind("@1poa_templates").Where({ecosystem:$ecosystem_id, id:$TemplateId}).Row()
        if !$template_map {
            error Sprintf(LangRes("@1poa_template_not_found", "en"), $Recipient)
        }
    }
    
    action {
        var i int params map
        while i < Len($ParamArr) {
            params[$ParamArr[i]] = $ValueArr[i]
            i = i + 1
        }
        
        var m map
        m["contract"] = $template_map["contract"]
        m["date_created"] = $block_time 
        m["date_expiration"] = UnixDateTime($date_expiration)
        m["deleted"] = 0
        m["ecosystem"] = $ecosystem_id
        m["params"] = params
        m["poa_recipient"] = getMemberInfo($recipient_id )
        m["poa_sender"] = getMemberInfo($key_id)
        m["template_id"] = $TemplateId
        $result = DBInsert("@1poa", m)
    }
}