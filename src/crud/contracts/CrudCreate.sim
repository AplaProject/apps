contract CrudCreate {
    data {
        tableName string
    }
    func toCamel(t string, lowerFirstLetter bool) string{
        Println("toCamel")
        var parts array i int
        parts = Split(t, "_")
        while i < Len(parts){
            if i == 0 && lowerFirstLetter {
                parts[i] = ToLower(parts[i])
            }else{
                parts[i] = ToUpper(Substr(parts[i], 0, 1)) + ToLower(Substr(parts[i], 1, Size(parts[i])))
            }
            i=i+1
        }
        return Join(parts, "")
    }
    func getPaginator() string{
        Println("getPaginator")
        var body string
        body = `
        DBFind(`+$tableName+`).Count(count)
        If(#page#>0){
            SetVar(prev_page,Calculate(#page#-1)
        }.Else{
            SetVar(page,0).(prev_page,0)
        }
        SetVar(per_page,25).(off,Calculate(#page#*#per_page#)).(last_page,Calculate(#count#/#per_page#)).(next_page,#last_page#)
        If(#count#>Calculate(#off#+#per_page#)){
            SetVar(next_page,Calculate(#page#+1)
        }
        Div(button-group){
            If(#page#>0){
                Button(Body:"1", Class:btn btn-default, Page:#page_name#, PageParams: "page=0")
            }.Else{
                Button(Body:"1", Class:btn btn-default disabled)
            }
            If(#page#>1){
                Button(Body:Calculate(#prev_page#+1),Class:btn btn-default, Page:#page_name#, PageParams:"page=#prev_page#")
            }
            If(And(#page#>0,#page#<#last_page#)){
                Button(Body:Calculate(#page#+1), Class:btn btn-default disabled)
            }
            If(#next_page#<#last_page#){
                Button(Body:Calculate(#next_page#+1),Class:btn btn-default,Page:#page_name#, PageParams:"page=#next_page#")
            }
            If(#page#<#last_page#){
                Button(Body:Calculate(#last_page#+1), Class:btn btn-default, Page:#page_name#, PageParams:"page=#last_page#")
            }.ElseIf(#last_page#>0){
                Button(Body:Calculate(#last_page#+1), Class:btn btn-default disabled)
            }
        }`

        return body
    }
    func getPageRecords(cols array, names map) string{
        Println("getPageRecords")
        var pageEdit pageView this contractDelete string
        pageEdit = names["pageEdit"]
        pageView = names["pageView"]
        this =  names["pageRecords"]
        contractDelete =  names["contractDelete"]
        var body string
        body = `DBFind(applications).Columns("id").Where("name='`+$appName+`' AND deleted=0").Vars(application)
        SetTitle($`+this+`$)
        SetVar(page_name,`+this+`)
        Div(content-wrapper){
            AddToolButton(Title: Create, Page: `+pageEdit+`, Icon: icon-plus)
            `+getPaginator()+`
            Div(panel panel-primary){
                Div(panel-body){
                    Div(table-responsive){
                        DBFind(`+$tableName+`,src_`+$tableName+`).Offset(#off#).Order(id).Custom(_actions){
                            Div(text-right text-nowrap){
                                Button(Class: btn btn-info fa fa-eye mr-sm, Page: `+pageView+`, PageParams: "`+$ID+`=#id#")
                                Button(Class: btn btn-default fa fa-edit mr-sm, Page: `+pageEdit+`, PageParams: "`+$ID+`=#id#")
                                Button(Class: btn btn-danger fa fa-trash, Page: `+this+`, Contract: `+contractDelete+`, Params: "`+$ID+`=#id#").Alert($delete_alert$, $yes$, $no$)
                            }
                        }
                        Table(src_`+$tableName+`,"`+Join(cols,",")+`,=_actions")
                    }
                }
            }
        }`
        return body
    }
    func trim_s(s string)string{
        Println("trim_s")
        if Substr(s, Size(s)-1, 1) == "s"{
            return Substr(s, 0, Size(s)-1)
        }
        return s
    }
    func getPageEdit(cols array, names map)string{
        Println("getPageEdit")
        var body pageRecords contractEdit string i lenCols int
        pageRecords =  names["pageRecords"]
        contractEdit =  names["contractEdit"]
        body = `DBFind(applications).Columns("id").Where("name='`+$appName+`' AND deleted=0").Vars(application)SetVar(title,$create$).(btn_title,$create$)`
        lenCols = Len(cols)
        while i < lenCols{
            body = body + `SetVar(item_`+cols[i]+`,)`
            i=i+1
        }
        body = body+`
        If(GetVar(`+$ID+`)){
            DBFind(`+$tableName+`).Where("id=#`+$ID+`#").Vars(item)
            SetVar(title,$edit$).(btn_title,$edit$)
        }
        SetTitle(#title#)
        Div(content-wrapper){
            Div(breadcrumb){
                LinkPage(Body: $`+pageRecords+`$, Page: `+pageRecords+`)
                Span(/, mh)
                Span(#title#, text-muted)
            }
            Div(row){
                Div(col-sm-8 col-sm-offset-2 col-lg-6 col-lg-offset-3){
                    Form(panel panel-primary){
                        Div(panel-heading){
                            LangRes(data_input)
                        }
                        Div(panel-body){
                            If(GetVar(`+$ID+`)){
                                Input(Name: `+$ID+`, Type:hidden, Value: #`+$ID+`#)
                            }`
                            i=0
                            while i<lenCols{
                                body = body+`
                                Div(row mt-sm){
                                    Div(col-sm-4 mt-sm text-right){
                                        LangRes(`+cols[i]+`)
                                    }
                                    Div(col-sm-8 text-left){
                                        Input(Name: `+toCamel(cols[i], $lowerFirst)+`, Type:`+$pageInputTypes[GetColumnType($tableName, cols[i])]+`, Value: #item_`+cols[i]+`#)
                                    }
                                }`
                                i=i+1
                            }
                            body = body+`
                        }
                        Div(panel-footer text-right){
                            Button(Body: Back, Page: `+pageRecords+`, Class: btn btn-default pull-left)
                            Button(Body: #btn_title#, Page: `+pageRecords+`, Class: btn btn-primary, Contract: `+contractEdit+`)
                        }
                    }
                }
            }
        }`
        return body
    }
    func getPageView(cols array, names map)string{
        Println("getPageView")
        var body pageRecords string i lenCols int
        pageRecords = names["pageRecords"]
        cols = Append(cols, "id")
        lenCols = Len(cols)
        body = body+`
        DBFind(applications).Columns("id").Where("name='`+$appName+`' AND deleted=0").Vars(application)
        If(GetVar(`+$ID+`)){
            DBFind(`+$tableName+`).Where("id=#`+$ID+`#").Vars(item)
        }
        SetTitle(title,$view$ ##`+$ID+`#)
        Div(content-wrapper){
            Div(breadcrumb){
                LinkPage(Body: $`+pageRecords+`$, Page: `+pageRecords+`)
                Span(/, mh)
                Span(#title#, text-muted)
            }
            If(GetVar(`+$ID+`)){
                Div(row){
                    Div(col-sm-8 col-sm-offset-2 col-lg-6 col-lg-offset-3){
                        Div(panel panel-primary){
                            Div(panel-heading){
                                LangRes(data_view)
                            }
                            Div(panel-body){`
                            i=0
                            while i<lenCols{
                                body = body+`
                                Div(row mt-sm){
                                    Div(col-sm-4 mt-sm text-right text-bold){
                                        LangRes(`+cols[i]+`)
                                    }
                                    Div(col-sm-8 text-left){
                                        #item_`+cols[i]+`#
                                    }
                                }
                                `
                                i=i+1
                            }
                            body = body+`}
                            Div(panel-footer text-left){
                                Button(Body: Back, Page: `+pageRecords+`, Class: btn btn-default)
                            }
                        }
                    }
                }
            }.Else{
                Div(h3 text-center){
                    LangRes(`+$prefix+`item_not_found)
                }
            }
        }`
        return body
    }
    func getPrefix(appName string)string{
        Println("getPrefix")
        var temp array
        temp = Split(appName, " ")
        return ToLower(temp[0]+"_")
    }
    func getMapValues(m map, keys array) array {
        Println("getMapValues")
        var values array i keysLen int key string
        keysLen = Len(keys)
        while i < keysLen {
            key = keys[i]
            values = Append(values, m[key])
            i=i+1
        }
        return values
    }
    func getContractDelete(name string, cols array)string{
        Println("getContractDelete")
        var body dColName string i lenCols int
        lenCols = Len(cols)
        while i<lenCols{
            if "delete" == cols[i] || "deleted" == cols[i]{
                dColName = cols[i]
            }
            i=i+1
        }
        if !dColName{
            return ""
        }
        body = `contract `+name+` {
            data {
                `+ $ID +` int
            }
            conditions {
                if !DBFind("`+$tableName+`").Where("id=? and `+dColName+`=0", $`+$ID+`).One("id") {
                    warning "Record not found"
                }
            }
            action {
                DBUpdate("`+$tableName+`", $`+ $ID +`, "`+dColName+`", 1)
            }
        }`
        return body
    }
    func globJoin(cols array)string{
        Println("globJoin")
        var body array i lenCols int
        lenCols = Len(cols)
        while i<lenCols{
            body = Append(body, "$"+toCamel(cols[i], $lowerFirst))
            i=i+1
        }
        return Join(body, ",")
    }
    func getContractEdit(name string, cols array)string{
        Println("getContractEdit")
        var body dColName string i lenCols int
        lenCols = Len(cols)
        body = `contract `+name+` {
            data {
                `+$ID+` string "optional"`
                while i<lenCols{
                    body = body+`
                    `+toCamel(cols[i], $lowerFirst)+` ` +$contractInputTypes[GetColumnType($tableName, cols[i])]
                    i=i+1
                }
                body = body + `
            }
            conditions {
                $`+$ID+` = Int($`+$ID+`)
                if $`+$ID+` > 0{
                    if !DBFind("`+$tableName+`").Where("id=?", $`+$ID+`).One("id") {
                        warning "Record not found"
                    }
                }
            }
            action {
                if $`+$ID+` > 0{
                    DBUpdate("`+$tableName+`", $`+$ID+`, "`+Join(cols, ",")+`",`+globJoin(cols)+`)
                }else{
                    DBInsert("`+$tableName+`", "`+Join(cols, ",")+`",`+globJoin(cols)+`)
                }
            }
        }`
        return body
    }

    func createExportItem(type, name, content string)map{
        Println("createExportItem")
        var item map
        if !(content && name && type){
            return item
        }
        if type == "pages"{
            item["Menu"] = "default_menu"
        }

        item["Conditions"] = "true"
        item["Value"] = content
        item["Name"] = name
        item["Type"] = type
        return item
    }
    func export(items ...){
        Println("export")
        var app item map i lenItems int ar array fileName string
        fileName = Sprintf("%v.json", Replace(ToLower($appName), " ", "_"))
        lenItems = Len(items)
        app["name"] = $appName
        while i<lenItems{
            item = items[i]
            if item["Value"] {
                ar = Append(ar, item)
            }
            i=i+1
        }
        app["data"] = ar
        @1UploadBinary("Name,Data,ApplicationId,DataMimeType", fileName, JSONEncode(app), $appId, "application/json")
    }
    conditions {
        $table = DBFind("tables").Columns("columns").Where("name=?", $tableName).Row()
        if !$table{
            warning "Table not found"
        }
        $lowerFirst = true // first letter for input global variables
        $ID = toCamel("id", $lowerFirst)

        $appId = Int(DBFind("tables").Where("name=?", $tableName).Columns("app_id").One("app_id"))
        $appName = DBFind("applications").Where("id=?", $appId).Columns("name").One("name")
        if !$appName {
            warning "Application not found"
        }
        var p1 p2 string
        $tableName = ToLower($tableName)
        $prefix = getPrefix($appName)
        $pagePrefix = $prefix + trim_s($tableName) + "_"
        p1 = toCamel($prefix, false)
        p2 = toCamel(trim_s($tableName), false)
        $contractPrefix = p1
        if p1 != p2{
            $contractPrefix = p1 + p2
        }

        var pageTypes contractTypes map
        pageTypes["text"] = "text"
        pageTypes["number"] = "number"
        pageTypes["money"] = "number"
        pageTypes["varchar"] = "text"
        pageTypes["datetime"] = "date"
        pageTypes["double"] = "number"
        pageTypes["character"] = "text"
        pageTypes["json"] = "textarea"
        pageTypes["bytea"] = "file"
        pageTypes["uuid"] = "text"

        contractTypes["text"] = "string"
        contractTypes["number"] = "int"
        contractTypes["money"] = "money"
        contractTypes["varchar"] = "string"
        contractTypes["datetime"] = "string"
        contractTypes["double"] = "float"
        contractTypes["character"] = "string"
        contractTypes["json"] = "string"
        contractTypes["bytea"] = "bytes"
        contractTypes["uuid"] = "string"

        $pageInputTypes = pageTypes
        $contractInputTypes = contractTypes
    }
    action {
        var cols colsNames array names map pageRecords pageEdit pageView contractDelete contractEdit string

        cols = JSONDecode($table["columns"])
        colsNames = GetMapKeys(cols)

        names["pageRecords"] = $prefix + $tableName
        names["pageEdit"] = $pagePrefix + "edit"
        names["pageView"] = $pagePrefix + "view"
        names["contractEdit"] = $contractPrefix + "Edit"
        names["contractDelete"] = $contractPrefix + "Delete"

        pageRecords = getPageRecords(colsNames, names)
        pageEdit = getPageEdit(colsNames, names)
        pageView = getPageView(colsNames, names)

        contractDelete = getContractDelete(names["contractDelete"], colsNames)
        contractEdit = getContractEdit(names["contractEdit"], colsNames)

        export(
            createExportItem("pages", names["pageRecords"], pageRecords),
            createExportItem("pages", names["pageEdit"], pageEdit),
            createExportItem("pages", names["pageView"], pageView),
            createExportItem("contracts", names["contractEdit"], contractEdit),
            createExportItem("contracts", names["contractDelete"], contractDelete)
        )
    }
}