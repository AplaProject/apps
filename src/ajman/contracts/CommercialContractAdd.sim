contract CommercialContractAdd {
    data {
        //vars from form
        total_rent_amount money
        security_deposit money
        no_of_installments int
        start_date string "date"
        end_date string "date"
        with_parking string "optional"
        no_of_renewal int "optional"
        terms_and_conditions_en string "optional"
        terms_and_conditions_ar string "optional"
        owner_signature_date string "date"
        tenant_signature_date string "date"
        //additional vars
        property_id int "hidden"
        unit_id int "hidden"
        company_id int "hidden optional"
        license_number int "hidden optional"
        application_number int "hidden optional"
    }
    conditions {
        if $total_rent_amount < 0 {
            error "Total Rent Amount can't be less than 0"
        }
        if $security_deposit < 0 {
            error "Security Deposit can't be less than 0"
        }
        if $no_of_installments < 0 {
            error "No. of Installments can't be less than 0"
        }
        if $start_date > $end_date {
            error "Start date is greater End date"
        }
        if ($no_of_renewal < 1 || $no_of_renewal > 3) && $no_of_renewal {
            error "No. of Renewal can be only 1,2 or 3"
        }
        if !$license_number && !$application_number {
            error "License number or Application number is required"
        }
        if $with_parking == "true" {
            $with_parking = "1"
        }else{
            $with_parking = "0"
        }
        if !$no_of_renewal {
            $no_of_renewal = 0
        }
        if !$company_id {
            $company_id = 0
        }
    }
    action {
        var duration int
        $duration = 0
        DBInsert("commercial_contracts", "property_id,unit_id,company_id,license_number,application_number,total_rent_amount,security_deposit,no_of_installments,start_date,end_date,with_parking,no_of_renewal,terms_and_conditions_en,terms_and_conditions_ar,owner_signature_date,tenant_signature_date,date_create,duration", $property_id, $unit_id, $company_id, $license_number, $application_number, $total_rent_amount, $security_deposit, $no_of_installments, $start_date, $end_date, $with_parking, $no_of_renewal, $terms_and_conditions_en, $terms_and_conditions_ar, $owner_signature_date, $tenant_signature_date,"NOW()",$duration)
    }
}