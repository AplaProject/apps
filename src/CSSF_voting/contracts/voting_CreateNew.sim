contract voting_CreateNew {
    data {
        voting_name string
        voting_type int
        type_participants int
        type_decision int

        volume int
        quorum int

        description string "optional"
        interval string "optional"
    }

    func conditions {
        var m string
        if ($volume < 50) || ($volume > 100) {
            m = Sprintf("Volume [%v] should be in the range from 50 to 100", $volume)
            warning m
        }

        if ($quorum < 5) || ($quorum > 100) {
            m = Sprintf("Quorum [%v] should be in the range from 5 to 100", $quorum)
            warning m
        }

        $key = DBFind("keys").Where("id=$", $key_id).Row()
        if !$key {
            warning "You are not a member of the ecosystem"
        }
        $date_started = $time + 5 * 60

        var days int
        days = Int($interval)
        if days == 0 {
            days = 3
        }
        $date_ended = $time + 3600*24*days
    }

    func action {
        var voting map
        voting["name"] = $voting_name
        voting["type"] = $voting_type
        voting["description"] = $description
        voting["type_participants"] = $type_participants
        voting["type_decision"] = $type_decision
        voting["volume"] = $volume
        voting["quorum"] = $quorum

        var progress map
        progress["number_participants"] = "0"
        progress["number_voters"] = "0"
        progress["percent_success"] = "0"
        progress["percent_voters"] = "0"

        var flags map
        flags["success"] = "0"
        flags["decision"] = "0"
        flags["full_data"] = "0"
        flags["notifics"] = "0"

        var creator map
        creator["member_id"] = $key["id"]
        creator["member_name"] = IdToAddress($key_id)

        var votingId int
        votingId = DBInsert("votings", "voting,progress,flags,creator,timestamp date_started,timestamp date_ended", voting, progress, flags, creator, $date_started, $date_ended)

        // if all members are voting participants
        if $type_participants == 1 {
            voting_Invite("votingID,var_id", votingId, 0)
        }
        $result = votingId
    }
}