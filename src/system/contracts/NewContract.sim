contract NewContract {
    data {
        ApplicationId int
        Value string
        Conditions string
        Wallet string "optional"
        TokenEcosystem int "optional"
        Lang string "optional"
    }

    conditions {
        ValidateCondition($Conditions,$ecosystem_id)

        if $ApplicationId == 0 {
            warning LangRes("@1aid_cannot_zero", $Lang)
        }

        $walletContract = $key_id
        if $Wallet {
            $walletContract = AddressToId($Wallet)
            if $walletContract == 0 {
                
                error Sprintf(LangRes("@1template_wrong_wallet", $Lang), $Wallet)
            }
        }

        $contract_name = ContractName($Value)

        if !$contract_name {
            error LangRes("@1must_name", $Lang)"must be the name"
        }

        if !$TokenEcosystem {
            $TokenEcosystem = 1
        } else {
            if !SysFuel($TokenEcosystem) {
                warning Sprintf(LangRes("@1template_eco_isnot_system", $Lang), $TokenEcosystem)
            }
        }
    }

    action {
        $result = CreateContract($contract_name, $Value, $Conditions, $walletContract, $TokenEcosystem, $ApplicationId)
    }
}