contract EditContract {
    data {
        Id int
        Value string "optional"
        Conditions string "optional"
        WalletId string "optional"
    }
    func onlyConditions() bool {
        return $Conditions && !$Value && !$WalletId
    }

    conditions {
        RowConditions("@1contracts", $Id, onlyConditions())
        if $Conditions {
            ValidateCondition($Conditions, $ecosystem_id)
        }
        $cur = DBFind("@1contracts").Columns("id,value,conditions,active,wallet_id,token_id").Where({id: $Id, ecosystem: $ecosystem_id}).Row()
        if !$cur {
            error Sprintf(LangRes("@1template_contract_not_exist", "en"), $Id)
        }
        if $Value {
            ValidateEditContractNewValue($Value, $cur["value"])
        }
        if $WalletId != "" {
            $recipient = AddressToId($WalletId)
            if $recipient == 0 {
                error Sprintf(LangRes("@1template_new_owner_invalid", "en"), $WalletId)
            }
            if Int($cur["active"]) == 1 {
                error LangRes("@1contract_must_deactivated", "en")
            }
        } else {
            $recipient = Int($cur["wallet_id"])
        }
    }

    action {
        UpdateContract($Id, $Value, $Conditions, $WalletId, $recipient, $cur["active"], $cur["token_id"])
    }
}