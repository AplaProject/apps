contract supplychains_supply_add_transport {
    data {
        cargo_id int
        transport_id int
    }

    conditions {
        $supply_map = DBFind("supplychains_supply").Where("cargo_id=?", $cargo_id).Row()
        if !$supply_map {
            warning "Supply not found"
        }

        $transport_map = DBFind("supplychains_transport").Where("id=?", $transport_id).Row()
        if !$transport_map {
            warning "Transport not found"
        }

        $step_map = DBFind("supplychains_steps").Where("supply_id=? and step=-1", Int($supply_map["id"])).Row()
        if !$step_map {
            warning "Step not found"
        }
    }

    action {
        DBUpdate("supplychains_supply", Int($supply_map["id"]), "step", 0)

        DBUpdate("supplychains_steps", Int($step_map["id"]), "step,receiver_id,receiver_name,receiver_role_id,receiver_type",
            0, $transport_id, Str($transport_map["name"]), Int($transport_map["role_id"]), 2)

        DBInsert("supplychains_steps", "supply_id, holder_id, holder_name, holder_type, holder_role_id, step",
            Int($supply_map["id"]), $transport_id, Str($transport_map["name"]), 2, Int($transport_map["role_id"]), 1)
    }
}