contract supplychains_step_add {
    data {
        cargo_id int
        transport_id int
        warehouse_id int
        param int
    }

    conditions {
        $supply_map = DBFind("supplychains_supply").Where("cargo_id=?", $cargo_id).Row()
        if !$supply_map {
            warning "Supply not found"
        }

        $step = Int($supply_map["step"])
        $step = $step + 1

        $step_map = DBFind("supplychains_steps").Where("supply_id=? and step=?", Int($supply_map["id"]), $step).Row()
        if !$step_map {
            warning "Step not found"
        }

        $var_id = 0
        $var_name = ""
        $var_role_id = 0;
        $var_type = 0

        if ($param == 1){
            $transport_map = DBFind("supplychains_transport").Where("id=?", $transport_id).Row()
            if !$transport_map {
                warning "Transport not found"
            }
            $var_id = $transport_id
            $var_name = Str($transport_map["name"])
            $var_role_id = Int($transport_map["role_id"]);
            $var_type = 2
		}
		
        if ($param == 2){
            $warehouse_map = DBFind("supplychains_warehouses").Where("id=?", $warehouse_id).Row()
            if !$warehouse_map {
                warning "Warehouse not found"
            }
            $var_id = $warehouse_id
            $var_name = Str($warehouse_map["name"])
            $var_role_id = Int($warehouse_map["role_id"]);
            $var_type = 3
        }
    }

    action {
        DBUpdate("supplychains_supply", Int($supply_map["id"]), "step", $step)

        DBUpdate("supplychains_steps", Int($step_map["id"]), "receiver_id,receiver_name,receiver_role_id,receiver_type",
            $var_id, $var_name, $var_role_id, $var_type)

        $step = $step + 1
        DBInsert("supplychains_steps", "supply_id, holder_id, holder_name, holder_type, holder_role_id, step",
            Int($supply_map["id"]), $var_id, $var_name, $var_type, $var_role_id, $step)
    }
}