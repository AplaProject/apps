contract voting_CreateNew {
    data {
        voting_name string
        voting_type int
        type_participants int
        type_decision int

        volume int
        quorum int

        now_date string "date"
        start_time string "date"
        start_date string "date"
        end_date string "date"
        end_time string "date"

        description string "optional"
    }

    func conditions {
        $date_started = $start_date + " " + $start_time
        $date_ended = $end_date + " " + $end_time
        var m string

        if ($now_date >= $date_started) {
            m = Sprintf("Voting start date [%v] is less than the current date [%v]", $date_started, $now_date)
            warning m
        }

        if ($date_started >= $date_ended) {
            m = Sprintf("Voting end date [%v] is less than the start date [%v]", $date_ended, $date_started)
            warning m
        }

        if (($volume < 50) || ($volume > 100)) {
            m = Sprintf("Volume [%v] should be in the range from 50 to 100", $volume)
            warning m
        }

        if (($quorum < 5) || ($quorum > 100)) {
            m = Sprintf("Quorum [%v] should be in the range from 5 to 100", $quorum)
            warning m
        }

        $member_map = DBFind("members").Where("id=$", $key_id).Row()
        if !$member_map {
            warning "You are not a member of the ecosystem"
        }
    }

    func action {
        var voting map
        voting["name"] = Str($voting_name)
        voting["type"] = Str($voting_type)
        voting["description"] = Str($description)
        voting["type_participants"] = Str($type_participants)
        voting["type_decision"] = Str($type_decision)
        voting["volume"] = Str($volume)
        voting["quorum"] = Str($quorum)

        var progress map
        progress["number_participants"] = "0"
        progress["number_voters"] = "0"
        progress["percent_success"] = "0"
        progress["percent_voters"] = "0"

        var flags map
        flags["success"] = "0"
        flags["decision"] = "0"
        flags["full_data"] = "0"
        flags["notifics"] = "0"

        var creator map
        creator["member_id"] = Str($member_map["id"])
        creator["member_name"] = Str($member_map["member_name"])
        creator["image_id"] = Str($member_map["image_id"])

        $voting_id = DBInsert("voting", "voting,progress,flags,creator,date_started,date_ended",
            voting, progress, flags, creator, $date_started, $date_ended)

        // if all members are voting participants
        if ($type_participants == 1) {
            voting_Invite("votingID,var_id", $voting_id, 0)
        }
    }
}