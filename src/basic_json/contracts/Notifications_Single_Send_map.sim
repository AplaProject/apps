contract Notifications_Single_Send_map {
    data {
        member_id int
        sender int
        icon_name string
        text_header string
        text_body string "optional"
        page_name string
        params_map map
    }

    conditions {
        $member_map = DBFind("members").Where("id = $", $member_id).Row()
        if !$member_map {
            warning "Member not found"
        }

        if $sender == 1 {
            $sender_map = DBFind("members").Where("id = $", $key_id).Row()
            if !$sender_map {
                warning "Member not found"
            }
        }
        if $sender == 2 {
            // !!! need to fix role_id to the global variable !!!!
            $sender_map = DBFind("roles").Where("id = $", 1).Row()
            if !$sender_map {
                warning "Member not found"
            }
        }
    }

    action {
        var recipient map
        recipient["member_id"] = Str($member_map["id"])
        recipient["member_name"] = Str($member_map["member_name"])
        recipient["image_id"] = Str($member_map["image_id"])

        var sender map
        if $sender == 1 {
            sender["type"] = "1"
            sender["member_id"] = Str($sender_map["id"])
            sender["member_name"] = Str($sender_map["member_name"])
            sender["image_id"] = Str($sender_map["image_id"])
        }
        if $sender == 2 {
            sender["type"] = "2"
            sender["role_id"] = Str($sender_map["id"])
            sender["role_name"] = Str($sender_map["role_name"])
            sender["image_id"] = Str($sender_map["image_id"])
        }

        var notification map
        notification["header"] = $text_header
        notification["type"] = "1"
        notification["icon"] = $icon_name
        notification["body"] = $text_body

        DBInsert("notifications", "recipient,sender,notification,page_name,page_params,timestamp date_created",
            recipient, sender, notification, $page_name, $params_map, $block_time)
    }
}