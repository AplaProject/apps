contract VotingSubjectCandidates {
    data {
        votingID int
        memberID int
    }
    
    func getMemberInfo(kid int) map {
        var res member map
        member = DBFind("@1members").Where({ecosystem:$ecosystem_id, id:kid}).Row()
        if member {
            res["member_id"] = member["id"]
            res["member_name"] = member["member_name"]
            res["image_id"] = member["image_id"]
        }else{
            member = DBFind("@1keys").WhereId(kid).Row()
            if member {
                res["member_id"] = member["id"]
                res["member_name"] = IdToAddress(Int(member["id"]))
                res["image_id"] = 0
            }
        }
        return res
    }

    conditions {
        $STARTED = 3
        $FINISHED = 4
        $voting = DBFind("@1votings").Where({ecosystem:$ecosystem_id, id:$votingID}).Columns("id,optional->number_candidates,voting->type,flags->full_data,status").Row()
        if $voting["status"] == $STARTED {
            warning LangRes("@1voting_started_can_not_add_candidates", "en")
        }
        if $voting["status"] == $FINISHED {
            warning LangRes("@1voting_finished_can_not_add_candidates", "en")
        }

        if $voting["flags.full_data"] == 1 && $voting["voting.type"] == 2 {
            warning LangRes("@1system_voting_can_not_edit", "en")
        }

        if $memberID == 0 {
            $memberID = $key_id
        }

        if DBFind("@1votings_subject").Where({ecosystem:$ecosystem_id, voting_id:$votingID, "subject->member_id":$memberID}).One("id") {
            warning LangRes("@1candidate_added_before", "en")
        }

        $member = getMemberInfo($memberID)
        if !$member {
            warning Sprintf(LangRes("@1key_not_found", "en") + " (%v)", $memberID)
        }
    }

    action {
        DBInsert("@1votings_subject", {ecosystem: $ecosystem_id, voting_id:$votingID, subject:$member})
        var m map
        m["optional->number_candidates"] = Int($voting["optional.number_candidates"]) + 1
        DBUpdate("@1votings", $votingID, m)
    }
}