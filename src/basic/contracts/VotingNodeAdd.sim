contract VotingNodeAdd {
    data {
        TcpAddress string
        ApiAddress string
        KeyId int
        PubKey string
        Duration int
    }
    
    func warn(name, detail string) {
        warning Sprintf(LangRes("@1x_invalid", "en") + " (%v)", name, detail)
    }
    
    func checkApiAddress(addr string) {
        var name string
        name = LangRes("@1api_address", "en")
        if Size(addr) == 0 {
            warn(name, "size")
        }
        if !(HasPrefix(addr, "http://") || HasPrefix(addr, "https://")) {
            warn(name, "protocol")
        }
        if Contains(addr, " ") {
            warn(name, "space")
        }
        addr = Replace(addr, "http://", "")
        addr = Replace(addr, "https://", "")
        var parts array
        if Contains(addr, ":") {
            parts = Split(addr, ":")
            if Len(parts) > 2 {
                warn(name, ":")
            }
            var port string
            port = parts[1]
            if port <= 0 {
                warn(name, "port")
            }
            addr = parts[0]
        }
    }
    
    func checkTcpAddress(addr string) {
        var name string
        name = LangRes("@1tcp_address", "en")
        if Size(addr) == 0 {
            warn(name, "size")
        }
        if Contains(addr, " ") {
            warn(name, "space")
        }
        var parts array
        if Contains(addr, ":") {
            parts = Split(addr, ":")
            if Len(parts) > 2 {
                warn(name, ":")
            }
            var port string
            port = parts[1]
            if port <= 0 {
                warn(name, "port")
            }
        }
    }
    
    func checkPub(pub string) {
        if PubToID(pub) == 0 {
            warning LangRes("@1public_key_invalid", "en")
        }
    }
    
    func checkKeyId(keyid int) {
        var existed map
        existed = DBFind("@1keys").Where({ecosystem:$ecosystem_id, id:keyid}).Row()
        if !existed {
            warning LangRes("@1key_not_found", "en")
        }
    }
    
    func getNodesJSON() string {
        var nodes string
        nodes = SysParamString("full_nodes")
        if Size(nodes) < 100 {
            nodes = "[]"
        }
        return nodes
    }
    
    func appendFirstNode(nodes array) array {
        var first string firstNode map
        first = AppParam($appId,"first_node", 1)
        if !HasPrefix(first, "{") || Size(first) < 100 {
            warning LangRes("@1first_node_invalid", "en")
        }
        firstNode = JSONDecode(first)
        checkTcpAddress(firstNode["tcp_address"])
        checkApiAddress(firstNode["api_address"])
        checkKeyId(Int(firstNode["key_id"]))
        checkPub(firstNode["public_key"])

        nodes = Append(nodes, firstNode)
        return nodes
    }
    
    conditions {
        $appId = Int(DBFind("@1applications").Where({ecosystem:1, name:"Basic"}).Columns("name,id").One("id"))
        $templateId = Int(AppParam($appId,"voting_sysparams_template_id", 1))
        if $templateId == 0 {
            warning LangRes("@1template_id_not_found", "en")
        }
        checkKeyId($KeyId)
        checkPub($PubKey)
        checkApiAddress($ApiAddress)
        checkTcpAddress($TcpAddress)
    }

    action {
        var nodes array node map
        node["tcp_address"] = $TcpAddress
        node["api_address"] = $ApiAddress
        node["key_id"] = Str($KeyId)
        node["public_key"] = $PubKey
        nodes = JSONDecode(getNodesJSON())
        if Len(nodes) == 0 {
            nodes = appendFirstNode(nodes)
        }
        nodes = Append(nodes, node)
        @1VotingTemplateRun("TemplateId,Duration,KeyId,TableWritten,RowId,ColumnName,NewValue,TextDocument", $templateId, $Duration, 0, "", "", "full_nodes", JSONEncode(nodes), "")
    }
}