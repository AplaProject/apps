contract VotingSubjectSettings {
    data {
        votingID int

        roleID int
        vacancies int

        contract_accept string "optional"
        contract_accept_params string "optional"
        contract_reject string "optional"
        contract_reject_params string "optional"
    }
    
    func safeJsonToMap(m string) map {
        var res map
        if Size(m) > 0 { res = JSONToMap(m) }
        return res
    }

    conditions {
        if $vacancies < 1 {
            warning LangRes("@1vacancies_number_less_one found", "en")
        }

        $STARTED = 3
        $FINISHED = 4
        $voting = DBFind("@1votings").Where({ecosystem:$ecosystem_id, id:$votingID, status:[{"$neq":$STARTED},{"$neq":$FINISHED}]}).Columns("optional,voting->type,flags->full_data").Row()

        if !$voting {
            warning LangRes("@1voting_started_can_not_edit", "en")
        }

        if $voting["flags.full_data"] == 1 && $voting["voting.type"] == 2 {
            warning LangRes("@1system_voting_can_not_edit", "en")
        }
        var role map
        role = DBFind("@1roles").Where({ecosystem:$ecosystem_id, id:$roleID, deleted:0}).Row()
        if !role {
            warning LangRes("@1specified_role_not_found", "en")
        }

        // roles_types: 1. assigned_role, 2. elective_role, 3. system_role, 4. company_role
        if !(role["role_type"] == 2 || role["role_type"] == 3) {
            warning LangRes("@1role_not_elective_or_removed", "en")
        }
    }

    action {
        var optional map
        optional = safeJsonToMap($voting["optional"])

        optional["contract_accept"] = $contract_accept
        optional["contract_accept_params"] = safeJsonToMap($contract_accept_params)
        optional["contract_reject"] = $contract_reject
        optional["contract_reject_params"] = safeJsonToMap($contract_reject_params)
        optional["role_id"] = $roleID
        optional["role_vacancies"] = $vacancies

        DBUpdate("@1votings", $votingID, {optional:optional})

        @1VotingSubjectCheck("votingID", $votingID)
    }
}