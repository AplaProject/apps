contract VotingTemplateCreate {
    data {
        Title string
        TypeParticipants int
        TypeVoting int
        TypeDecision int
        Volume int
        Quorum int
        Subject string
        Voters string "optional" // role of participant
        Candidates string "optional" // role of candidate
        InitContract string "optional" // Execute before the vote
        Vacancies int "optional"
        ContractAccept string "optional"
        ParamsAccept string "optional"
        ContractReject string "optional"
        ParamsReject string "optional"
        CountTypeVoters int
    }

    conditions {
        if $TypeParticipants == 3 {
            if Size($Voters) == 0 || $Voters == "NULL" || $Voters == 0 {
                warning LangRes("@1voters_role_not_found", "en")
            }
        }
        if Size($Title) <3 {
            warning LangRes("@1enter_title", "en")
        }
        if $Volume <0 || $Volume > 100 {
            warning LangRes("@1volume_range_wrong", "en")
        }
        if $Quorum <0 || $Quorum > 100 {
            warning LangRes("@1quorum_range_wrong", "en")
        }

        if $TypeDecision == 1 || $TypeDecision == 2 {
            if $Vacancies < 1 {
                warning LangRes("@1vacancies_number_wrong", "en")
            }
            if $Candidates == "NULL" || $Candidates == 0 || Size($Candidates) == 0 {
                warning LangRes("@1candidate_role_wrong", "en")
            }
        }

        if Size($InitContract) > 0{
            if GetContractByName($InitContract) == 0 {
                warning LangRes("@1init_contract_not_found", "en")
            }
        }
        if Size($ContractAccept) > 0{
            if GetContractByName($ContractAccept) == 0 {
                warning LangRes("@1accepted_contract_not_found", "en")
            }
        }
        if Size($ContractReject) > 0{
            if GetContractByName($ContractReject) == 0{
                warning LangRes("@1rejected_contract_not_found", "en")
            }
        }
    }

    action {
        var m map
        m["title"] = $Title
        m["subject"] = $Subject
        m["voters"] = $Voters
        m["candidates"] = $Candidates
        m["init_contract"] = $InitContract
        m["volume"] = $Volume
        m["quorum"] = $Quorum
        m["type_voting"] = $TypeVoting
        m["type_participants"] = $TypeParticipants
        m["type_decision"] = $TypeDecision
        m["vacancies"] = $Vacancies
        m["contract_accept"] = $ContractAccept
        m["accept_params"] = $ParamsAccept
        m["contract_reject"] = $ContractReject
        m["reject_params"] = $ParamsReject
        m["ecosystem"] = $ecosystem_id
        m["count_type_voters"] = $CountTypeVoters
        $result = DBInsert("@1voting_templates", m)
    }
}