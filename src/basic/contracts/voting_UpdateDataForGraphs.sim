contract voting_UpdateDataForGraphs {
    data {
        votingID int
    }

    conditions {
		$voting_map = DBFind("voting_instances").Where("id=$", $votingID).Row()
		if ($voting_map["id"] == nil){
			warning "Voting is not found"
		}	
		
		$number_participants = Int($voting_map["number_participants"])  // number of participants
		$number_voters       = Int($voting_map["number_voters"])        // number of voters
		$volume              = Int($voting_map["volume"])               // percentage of votes for this vote
    }

    action {
		// increment count of voters
		$number_voters = $number_voters + 1
		
		// calculate the percentage of the ratio of the number of voters to the number of participants (for the graph)
		$percent_voters = ($number_voters * 100) / $number_participants
		if ($percent_voters > 100) {$percent_voters = 100}
		
		// update progress
		$percent_success = ($percent_voters * 100) / $volume
		if ($percent_success > 100) {$percent_success = 100}
		
		// update the flag of success
		$flag_success = 0
		if ($percent_success == 100) {$flag_success = 1}

		DBUpdate("voting_instances", $votingID, "number_voters,percent_voters,percent_success,flag_success", 
				$number_voters, $percent_voters, $percent_success, $flag_success)
    } 
}