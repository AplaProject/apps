contract VotingCreate {
    data {
        voting_name string
        voting_type int
        type_participants int
        type_decision int

        volume int
        quorum int "optional"

        count_type_voters string "optional"
        description string "optional"
        interval string "optional"
        start_date string "optional"
        start_time string "optional"
        end_date string "optional"
        end_time string "optional"
    }
    
    func trimZeroTime(s string) string {
        if Contains(s, "T00:00:00Z") {
            s = s Replace(s, "T00:00:00Z", "")
        }
        return s
    }
    
    func dateAddTime(d, t string) string {
        var dt string
        if Size(t) == 5 {
            dt = Sprintf("%v %v:00", trimZeroTime(d), t)
        }
        return dt
    }
    
    func isCalledFromPage() bool {
        return $stack[0] == Sprintf("@%v%v", $ecosystem_id, $this_contract)
    }
    
    func fixDatetimes() {
        $date_started = dateAddTime($start_date, $start_time)
        $date_ended = dateAddTime($end_date, $end_time)

        if UnixDateTime($date_started) == 0 || UnixDateTime($date_ended) == 0 { // invalid datetimes
            if isCalledFromPage(){
                var errs array
                if Size($start_date) < 10 {
                    errs = Append(errs, "starting date")
                }
                if Size($start_time) < 5 {
                    errs = Append(errs, "starting time")
                }
                if Size($end_date) < 10 {
                    errs = Append(errs, "ending date")
                }
                if Size($end_time) < 5 {
                    errs = Append(errs, "ending time")
                }
                error Sprintf(LangRes("@1voting_creation_error_x_unspecified", "en"), Join(errs, ", "))
            }else{
                $date_started = $block_time
                $interval = Int($interval)
                if $interval == 0 {
                    $interval = 3
                }
                $date_ended = $date_started + 3600 * 24 * $interval
                $date_started = DateTime($date_started)
                $date_ended = DateTime($date_ended)
            }
        }
        if UnixDateTime($date_started) < $block_time{
            error LangRes("@1starting_date_invalid", "en")
        }
        if UnixDateTime($date_started) >= UnixDateTime($date_ended){
            error LangRes("@1ending_date_invalid", "en")
        }
    }

    conditions {
        if (($type_decision == 1 || $type_decision == 2) && $count_type_voters == 1){
            info LangRes("@1voting_type_not_suitable", "en")
        }
        if $count_type_voters == 0 {
            if ($quorum < 5) || ($quorum > 100) {
                warning Sprintf(LangRes("@1quorum_range_must_be", "en"), $quorum)
            }
            if ($volume < 50) || ($volume > 100) {
                warning Sprintf(LangRes("@1volume_range_must_be", "en"), $volume)
            }
        }
        if $count_type_voters == 1 {
            if $volume <= 0 {
                info LangRes("@1voters_number_less_one", "en")
            }
            $quorum = 0
        }
        if $voting_type == 2 {
            var specRules map
            specRules["VotingTokensRefundCreate"] = 1
            specRules["VotingTemplateRun"] = 1
            specRules["VotingNodeAdd"] = 1
            if specRules[$original_contract] != 1 {
                warning Sprintf(LangRes("@1system_voting_can_not_start_from_x", "en"), $original_contract)
            }
        }


        $key = DBFind("@1keys").Where({ecosystem:$ecosystem_id, id:$key_id}).Row()
        if !$key {
            warning LangRes("@1ecosystem_not_member", "en")
        }

        fixDatetimes()
    }

    action {
        var voting progress flags creator map votingId int
        voting["name"] = $voting_name
        voting["type"] = $voting_type
        voting["description"] = $description
        voting["type_participants"] = $type_participants
        voting["type_decision"] = $type_decision
        voting["volume"] = $volume
        voting["quorum"] = $quorum
        voting["count_type_voters"] = $count_type_voters

        progress["number_participants"] = "0"
        progress["number_voters"] = "0"
        progress["percent_success"] = "0"
        progress["percent_voters"] = "0"

        flags["success"] = "0"
        flags["decision"] = "0"
        flags["full_data"] = "0"
        flags["notifics"] = "0"

        creator["member_id"] = $key["id"]
        creator["member_name"] = IdToAddress($key_id)
        var m map
        m["voting"] = voting
        m["progress"] = progress
        m["flags"] = flags
        m["creator"] = creator
        m["status"] = 1
        m["date_started"] = $date_started
        m["date_ended"] = $date_ended
        m["ecosystem"] = $ecosystem_id,

        votingId = DBInsert("@1votings", m)

        // if all members are voting participants
        if $type_participants == 1 {
            @1VotingInvite("votingID,var_id", votingId, 0)
        }
        $result = votingId
    }
}