contract RolesAccessManager {
    data {
        Action string
        Rid int
        ManagerRid string "optional"
    }
    
    func updateAccess(ra array) {
        var roles_access string
        roles_access = JSONEncode(ra)
        DBUpdate("@1roles", $Rid, {"roles_access": roles_access})
    }
    
    func updateBufferAccess(ra array) {
        var roles_access string
        roles_access = JSONEncode(ra)
        if !$buffer_id {
            DBInsert("@1buffer_data", {ecosystem:$ecosystem_id, "member_id": $key_id, "key": "roles_access", "value": roles_access})
        } else {
            DBUpdate("@1buffer_data", Int($buffer_id), {"value": roles_access})
        }
    }
    
    conditions {
        $ManagerRid = Int($ManagerRid)
        if $Rid > 0 {
            @1RolesAccessCheck("rid", $Rid)

            $role = DBFind("@1roles").Where({ecosystem:$ecosystem_id, id:$Rid}).Columns("id,roles_access").Row()
            if !$role {
                warning LangRes("@1role_not_found", "en")
            }
            if $ManagerRid > 0 {
                if !DBFind("@1roles").Where({ecosystem:$ecosystem_id, id:$ManagerRid}).One("id") {
                    warning LangRes("@1role_not_found", "en")
                }
            }
            var ids array, i int
            $ids = ids
            ids = JSONDecode($role["roles_access"])
            while i < Len(ids){
                if ids[i] > 0 {
                    $ids = Append($ids, ids[i])
                }
                i = i + 1
            }
        } else {
            $buffer_managers = 0
            $buffer_id = 0
            $ids = []
            var buffer map
            buffer = DBFind("@1buffer_data").Where({ecosystem:$ecosystem_id, "member_id": $key_id, "key": "roles_access"}).Row()
            if buffer {
                $buffer_managers = buffer["value"]
                $buffer_id = buffer["id"]
            }
        }
    }

    action {
        if $Rid == 0 {
            if $buffer_managers {
                $ids = JSONDecode($buffer_managers)
            }
        }
        if $Action == "clean" {
            updateAccess([])
        }
        if $ManagerRid > 0 {
            var i int, idsNew array
            if $Action == "remove" {
                while i < Len($ids) {
                    if $ids[i] != $ManagerRid{
                        idsNew = Append(idsNew, $ids[i])
                    }
                    i = i + 1
                }
                if $Rid == 0 {
                    updateBufferAccess(idsNew)
                } else {
                    updateAccess(idsNew)
                }
            }
            if $Action == "add" {
                var double bool
                while i < Len($ids) {
                    if $ids[i] == $ManagerRid{
                        double = true
                    }
                    i = i + 1
                }
                if !double {
                    $ids = Append($ids, $ManagerRid)
                }
                if $Rid == 0 {
                    updateBufferAccess($ids)
                } else {
                    updateAccess($ids)
                }
            }
        }
    }
}