contract voting_UpdateDataForGraphs {
    data {
        votingID int
    }
    
    func safeJsonToMap(m string) map {
        var res map
        if Size(m) > 0 { res = JSONToMap(m) }
        return res
    }
  
    func percentPartOfTotal(part, total int) int {
        if total == 0{ total = 1 }
        var percent int
        percent = Int((part * 100)/total)
        if percent > 100 { percent = 100 }
        return percent
    }
    
    conditions {
        $voting_map = DBFind("votings").Columns("flags,voting,progress").Where("id=?", $votingID).Row()
        if !$voting_map {
            warning "Voting is not found"
        }
    }
    
    action {
        var progress voting flags map
        progress = safeJsonToMap($voting_map["progress"])
        voting = safeJsonToMap($voting_map["voting"])
        flags = safeJsonToMap($voting_map["flags"])
        
        var flag_success percent_success percent_voters number_voters quorum  volume int
        // increment count of voters
        quorum = Int(voting["quorum"])
        volume = Int(voting["volume"])
        number_voters = Int(progress["number_voters"]) + 1
        percent_voters = percentPartOfTotal(number_voters, Int(progress["number_participants"]))
        percent_success = percentPartOfTotal(percent_voters, volume)
        
        // update the flag of success
        if percent_success == 100 {
            flag_success = 1
        }
        progress["percent_voters"] = percent_voters
        progress["percent_success"] = percent_success
        progress["number_voters"] = number_voters
        flags["success"] = flag_success
        
        DBUpdate("votings", $votingID, "progress,flags", progress, flags)
        
        if (percent_voters >= quorum && quorum > 50 && percent_voters >= volume) || percent_voters == 100 {
            // if a quorum is more than 50% we can immediately make the decision
            // OR
            // if all participants have voted we can not wait until the end of the voting period
            
            voting_CheckDecision("votingID", $votingID)
        }
    }
}